load("@rules_cc//cc:defs.bzl", "cc_library", "objc_library")
load("//third_party/qt:build_defs.bzl", "qt_moc", "qt_repc", "qt_uic")
load("//tools/project:build_defs.bzl", "mkstamp")

DEFINES = [
    "DEBUG",
    "LOG_TO_FILE",
    "QTOX_PLATFORM_EXT",
    "QT_MESSAGELOGCONTEXT",
    "UPDATE_CHECK_ENABLED",
]

qt_repc(
    name = "src_repc_source",
    srcs = ["platform/sandbox.rep"],
    tags = ["qt"],
    type = "source",
)

qt_repc(
    name = "src_repc_replica",
    srcs = ["platform/sandbox.rep"],
    tags = ["qt"],
    type = "replica",
)

qt_moc(
    name = "src_moc",
    srcs = [
        "appmanager.h",
        "chatlog/chatlinecontent.h",
        "chatlog/chatlinecontentproxy.h",
        "chatlog/chatwidget.h",
        "chatlog/content/broken.h",
        "chatlog/content/filetransferwidget.h",
        "chatlog/content/notificationicon.h",
        "chatlog/content/spinner.h",
        "chatlog/content/text.h",
        "chatlog/content/timestamp.h",
        "chatlog/customtextdocument.h",
        "chatlog/imageloader.h",
        "core/core.h",
        "core/coreav.h",
        "core/corefile.h",
        "core/toxcall.h",
        "ipc.h",
        "model/about/aboutfriend.h",
        "model/chat.h",
        "model/chathistory.h",
        "model/chatroom/conferenceroom.h",
        "model/chatroom/friendchatroom.h",
        "model/conference.h",
        "model/conferencemessagedispatcher.h",
        "model/debug/debuglogmodel.h",
        "model/debug/debugobjecttreemodel.h",
        "model/friend.h",
        "model/friendlist/friendlistmanager.h",
        "model/friendmessagedispatcher.h",
        "model/ichatlog.h",
        "model/imessagedispatcher.h",
        "model/notificationgenerator.h",
        "model/profile/profileinfo.h",
        "model/sessionchatlog.h",
        "net/avatarbroadcaster.h",
        "net/bootstrapnodeupdater.h",
        "net/toxuri.h",
        "net/updatecheck.h",
        "nexus.h",
        "persistence/db/rawdatabase.h",
        "persistence/history.h",
        "persistence/offlinemsgengine.h",
        "persistence/profile.h",
        "persistence/settings.h",
        "persistence/smileypack.h",
        "platform/desktop_notifications/desktopnotify.h",
        "platform/desktop_notifications/desktopnotifybackend.h",
        "platform/posixsignalnotifier.h",
        "platform/sandbox.h",
        "platform/sandboxclient.h",
        "platform/sandboxserver.h",
        "video/camerasource.h",
        "video/corevideosource.h",
        "video/netcamview.h",
        "video/videosource.h",
        "video/videosurface.h",
        "widget/about/aboutfriendform.h",
        "widget/categorywidget.h",
        "widget/chatformheader.h",
        "widget/circlewidget.h",
        "widget/conferencewidget.h",
        "widget/contentdialog.h",
        "widget/contentdialogmanager.h",
        "widget/emoticonswidget.h",
        "widget/form/addfriendform.h",
        "widget/form/chatform.h",
        "widget/form/conferenceform.h",
        "widget/form/conferenceinviteform.h",
        "widget/form/conferenceinvitewidget.h",
        "widget/form/debug/debuglog.h",
        "widget/form/debug/debugobjecttree.h",
        "widget/form/debugwidget.h",
        "widget/form/filesform.h",
        "widget/form/filetransferlist.h",
        "widget/form/genericchatform.h",
        "widget/form/loadhistorydialog.h",
        "widget/form/profileform.h",
        "widget/form/searchsettingsform.h",
        "widget/form/setpassworddialog.h",
        "widget/form/settings/aboutform.h",
        "widget/form/settings/advancedform.h",
        "widget/form/settings/avform.h",
        "widget/form/settings/generalform.h",
        "widget/form/settings/genericsettings.h",
        "widget/form/settings/privacyform.h",
        "widget/form/settings/userinterfaceform.h",
        "widget/form/settings/verticalonlyscroller.h",
        "widget/form/settingswidget.h",
        "widget/form/tabcompleter.h",
        "widget/friendlistlayout.h",
        "widget/friendlistwidget.h",
        "widget/friendwidget.h",
        "widget/genericchatitemwidget.h",
        "widget/genericchatroomwidget.h",
        "widget/loginscreen.h",
        "widget/maskablepixmapwidget.h",
        "widget/notificationedgewidget.h",
        "widget/passwordedit.h",
        "widget/qrwidget.h",
        "widget/searchform.h",
        "widget/style.h",
        "widget/tool/activatedialog.h",
        "widget/tool/adjustingscrollarea.h",
        "widget/tool/callconfirmwidget.h",
        "widget/tool/chattextedit.h",
        "widget/tool/croppinglabel.h",
        "widget/tool/flyoutoverlaywidget.h",
        "widget/tool/messageboxmanager.h",
        "widget/tool/profileimporter.h",
        "widget/tool/removechatdialog.h",
        "widget/tool/screengrabberchooserrectitem.h",
        "widget/tool/screengrabberoverlayitem.h",
        "widget/tool/screenshotgrabber.h",
        "widget/tool/toolboxgraphicsitem.h",
        "widget/widget.h",
        ":src_repc_replica",
        ":src_repc_source",
    ],
    hdrs = glob(["**/*.h"]),
    mocopts = [
        "-Iqtox",
        "-Iqtox/src",
        "-Iqtox/util/include",
    ] + ["-D%s=1" % d for d in DEFINES],
    tags = ["qt"],
    deps = [
        "//qtox/util",
        "@qt//:qt_remoteobjects",
        "@qt//:qt_widgets",
    ],
)

qt_uic(
    name = "src_ui",
    srcs = glob(["**/*.ui"]),
    tags = ["qt"],
)

objc_library(
    name = "avfoundation",
    hdrs = [
        "platform/camera/avfoundation.h",
        "video/videomode.h",
    ],
    copts = ["-Wno-vla-extension"],
    includes = ["."],
    non_arc_srcs = ["platform/camera/avfoundation.mm"],
    sdk_frameworks = ["AVFoundation"],
    tags = ["macos"],
    deps = ["@qt//:qt_core"],
)

mkstamp(
    name = "version",
    src = "version.cpp.in",
    out = "version.cpp",
)

COPTS = [
    "-Iqtox",
    "-I$(GENDIR)/qtox",
    "-I$(GENDIR)/qtox/src/platform",
] + select({
    "//tools/config:freebsd": [],
    "//tools/config:linux": ["-fPIC"],
    "//tools/config:osx": [],
    "//tools/config:windows": [],
})

cc_library(
    name = "serialize",
    srcs = ["persistence/serialize.cpp"],
    hdrs = ["persistence/serialize.h"],
    copts = COPTS,
    tags = ["qt"],
    visibility = ["//qtox:__subpackages__"],
    deps = ["@qt//:qt_core"],
)

cc_library(
    name = "src",
    srcs = [":version"] + [
        ":src_moc",
        ":src_repc_replica",
        ":src_repc_source",
        ":src_ui",
    ] + glob(
        ["**/*.cpp"],
        exclude = [
            "persistence/serialize.cpp",
            "main.cpp",
        ],
    ),
    hdrs = glob(
        ["**/*.h"],
        exclude = ["persistence/serialize.h"],
    ),
    copts = COPTS,
    defines = DEFINES,
    includes = ["."],
    linkopts = select({
        "//tools/config:freebsd": [],
        "//tools/config:linux": [],
        "//tools/config:osx": ["-framework IOKit"],
        "//tools/config:windows": [],
    }),
    tags = ["qt"],
    visibility = ["//qtox:__subpackages__"],
    deps = [
        ":serialize",
        "//c-toxcore",
        "//qtox:res_lib",
        "//qtox/audio",
        "//qtox/util",
        "@ffmpeg",
        "@libexif",
        "@libqrencode",
        "@libsodium",
        "@libvpx",
        "@openal",
        "@qt//:qt_concurrent",
        "@qt//:qt_core",
        "@qt//:qt_dbus",
        "@qt//:qt_gui",
        "@qt//:qt_network",
        "@qt//:qt_remoteobjects",
        "@qt//:qt_svg",
        "@qt//:qt_widgets",
        "@qt//:qt_xml",
        "@sqlcipher",
    ] + select({
        "//tools/config:freebsd": [],
        "//tools/config:linux": [
            "@x11",
            "@xss",
        ],
        "//tools/config:osx": [":avfoundation"],
        "//tools/config:windows": [],
    }),
)

cc_library(
    name = "main",
    srcs = ["main.cpp"],
    copts = COPTS,
    tags = ["qt"],
    visibility = ["//qtox:__subpackages__"],
    deps = [
        ":src",
        "//qtox/audio",
        "@qt//:qt_core",
        "@qt//:qt_gui",
    ],
)
